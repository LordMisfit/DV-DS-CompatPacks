/////Zakachi Decorate\\\\\

ACTOR Zakachi : AetheriusMonsterZSC 27043 
{
	OBITUARY "%o witnessed the true power of Zakachi Daemonbourne."
	Health 48000
	Radius 25
	Height 75 //50
	Mass 200
	Speed 12 //4
	PainChance 25 //100
	SEESOUND "thor/sight"
	PAINSOUND "thor/pain"
	DEATHSOUND "thor/death"
	ACTIVESOUND "thor/active"
	BloodColor "00 00 20"
	MONSTER
	+Boss
	+FloorClip
	+NoRadiusDMG
	+DontMorph
	+MissileMore
	+NoIceDeath
	states
	{
		Spawn:
			TNT1 A 0 A_Look
			THOR M 0 A_CustomMissile("LightningBallC1",200,0,0,2,0)
			THOR M 0 A_CustomMissile("LightningBallC2",200,0,180,2,0)
			Goto Idle
		Idle:
			THOR M 5 A_Look
			Loop
		See:
			TNT1 A 0 Thing_ChangeTID(0,1222)
			THOR MNOP 5
			{
				A_UnSetReflectiveInvulnerable;

				if (levelbehavior > 0 && user_monsterlevel > 180) { A_ChangeFlag("AVOIDMELEE",1); A_SetTics(3); }
				else if (levelbehavior > 0 && user_monsterlevel > 120) { A_ChangeFlag("AVOIDMELEE",1); A_SetTics(4); }
				else if (levelbehavior > 0 && user_monsterlevel > 60) { A_ChangeFlag("AVOIDMELEE",1); A_SetTics(5); }

				if (levelbehavior > 0) { user_fastchaseextra = user_monsterlevel/9; }
				user_fastchasechance = 16 + user_fastchaseextra + user_painfastchase;
				if (random(1,256) <= user_fastchasechance) { A_Chase("Melee","Missile",CHF_FASTCHASE); } else { A_Chase; }
				user_painfastchase = 0;
			}
			Loop
		Missile:
			TNT1 A 0 ACS_NamedExecuteAlways("RefreshStealthInfo",0,1) // makes a Stealth Monster become more visible
			TNT1 A 0 A_PlaySound("thor/attack")
			TNT1 A 0 A_Jump(64, "LightningColumns")
			TNT1 A 0 A_Jump(96, "GroundSeekers")
			TNT1 A 0 A_Jump(64, "Filibuster")
			TNT1 A 0 A_Jump(128, "LightningStream")
			Goto LightningBall
		//attack 1; fragmenting lightning ball
		LightningBall:
			THOR R 5
			{
				if (levelbehavior > 0 && user_monsterlevel > 180) { A_SetTics(3); }
				else if (levelbehavior > 0 && user_monsterlevel > 90) { A_SetTics(4); }
			}
			THOR S 5
			{
				if (levelbehavior > 0 && user_monsterlevel > 180) { A_SetTics(3); }
				else if (levelbehavior > 0 && user_monsterlevel > 90) { A_SetTics(4); }
				A_FaceTarget;
			}
			THOR T 5 BRIGHT
			{
				if (levelbehavior > 0 && user_monsterlevel > 180) { A_SetTics(3); }
				else if (levelbehavior > 0 && user_monsterlevel > 90) { A_SetTics(4); }

				if (levelbehavior > 0 && user_monsterlevel > 240)
				{
					A_CustomMissile("LightningBallL",32,0,8,2,0);
					A_CustomMissile("LightningBallL",32,0,0,2,0);
					A_CustomMissile("LightningBallL",32,0,-8,2,0);
				}
				else
				if (levelbehavior > 0 && user_monsterlevel > 120)
				{
					A_CustomMissile("LightningBallL",32,0,8,2,0);
					A_CustomMissile("LightningBallL",32,0,-8,2,0);
				}
				else
				{
					A_CustomMissile("LightningBallL",32,0,0,2,0);
				}
			}
			THOR T 5
			{
				if (levelbehavior > 0 && user_monsterlevel > 180) { A_SetTics(3); }
				else if (levelbehavior > 0 && user_monsterlevel > 90) { A_SetTics(4); }
			}
			Goto See
		//attack 2; lightning streams
		LightningStream:
			THOR RST 5 
			{
				if (levelbehavior > 0 && user_monsterlevel > 180) { A_SetTics(3); }
				else if (levelbehavior > 0 && user_monsterlevel > 90) { A_SetTics(4); }
				A_FaceTarget;
			}
			THOR TTTTTT 1 BRIGHT
			{
				A_CustomMissile("MiniLightningPuff",55,45,0,0);
				A_CustomMissile("MiniLightningPuff",55,-45,0,0);
				if (levelbehavior > 0 && user_monsterlevel > 120)
				{
					A_CustomMissile("MiniLightningPuff",55,67.5,0,0);
					A_CustomMissile("MiniLightningPuff",55,-67.5,0,0);
				}
			}
			TNT1 A 0 A_GiveInventory("ThorCounter", 1)
			TNT1 A 0 A_JumpIfInventory("ThorCounter", 9, "EndStream")
			Goto LightningStream+2
		EndStream:
			TNT1 A 0 A_TakeInventory("ThorCounter", 9)
			THOR T 5
			{
				if (levelbehavior > 0 && user_monsterlevel > 180) { A_SetTics(3); }
				else if (levelbehavior > 0 && user_monsterlevel > 90) { A_SetTics(4); }
			}
			Goto See
		//attack 3, stationary/invulnerable mode
		Filibuster:
			THOR ZY 5 
			{
				if (levelbehavior > 0 && user_monsterlevel > 180) { A_SetTics(3); }
				else if (levelbehavior > 0 && user_monsterlevel > 90) { A_SetTics(4); }
				A_FaceTarget;
			}
			THOR X 8 BRIGHT
			{
				if (levelbehavior > 0 && user_monsterlevel > 240) { A_SetTics(4); }
				else if (levelbehavior > 0 && user_monsterlevel > 180) { A_SetTics(5); }
				else if (levelbehavior > 0 && user_monsterlevel > 120) { A_SetTics(6); }
				else if (levelbehavior > 0 && user_monsterlevel > 60) { A_SetTics(7); }
				A_FaceTarget;
			}
			THOR Z 0 
			{
				A_CustomMissile("GroundLightning",0,0,0,0,0);
				A_CustomMissile("GroundLightning",0,0,45,0,0);
				A_CustomMissile("GroundLightning",0,0,90,0,0);
				A_CustomMissile("GroundLightning",0,0,135,0,0);
				A_CustomMissile("GroundLightning",0,0,180,0,0);
				A_CustomMissile("GroundLightning",0,0,225,0,0);
				A_CustomMissile("GroundLightning",0,0,270,0,0);
				A_CustomMissile("GroundLightning",0,0,315,0,0);
				if (levelbehavior > 0 && user_monsterlevel > 120)
				{
					A_CustomMissile("GroundLightning",0,0,22.5,0,0);
					A_CustomMissile("GroundLightning",0,0,67.5,0,0);
					A_CustomMissile("GroundLightning",0,0,112.5,0,0);
					A_CustomMissile("GroundLightning",0,0,157.5,0,0);
					A_CustomMissile("GroundLightning",0,0,202.5,0,0);
					A_CustomMissile("GroundLightning",0,0,247.5,0,0);
					A_CustomMissile("GroundLightning",0,0,292.5,0,0);
					A_CustomMissile("GroundLightning",0,0,337.5,0,0);
				}
				A_SetReflectiveInvulnerable;
				A_SetTranslucent(0.5, 1);
				A_ChangeFlag("Visibilitypulse", 1);
			}
			Goto FilibusterLoop
		FilibusterLoop:    
			THOR Z 35 
			{
				if (levelbehavior > 0 && user_monsterlevel > 240) { A_SetTics(18); }
				else if (levelbehavior > 0 && user_monsterlevel > 228) { A_SetTics(19); }
				else if (levelbehavior > 0 && user_monsterlevel > 216) { A_SetTics(20); }
				else if (levelbehavior > 0 && user_monsterlevel > 204) { A_SetTics(21); }
				else if (levelbehavior > 0 && user_monsterlevel > 192) { A_SetTics(22); }
				else if (levelbehavior > 0 && user_monsterlevel > 180) { A_SetTics(23); }
				else if (levelbehavior > 0 && user_monsterlevel > 168) { A_SetTics(24); }
				else if (levelbehavior > 0 && user_monsterlevel > 156) { A_SetTics(25); }
				else if (levelbehavior > 0 && user_monsterlevel > 144) { A_SetTics(26); }
				else if (levelbehavior > 0 && user_monsterlevel > 132) { A_SetTics(27); }
				else if (levelbehavior > 0 && user_monsterlevel > 120) { A_SetTics(28); }
				else if (levelbehavior > 0 && user_monsterlevel > 108) { A_SetTics(29); }
				else if (levelbehavior > 0 && user_monsterlevel > 96) { A_SetTics(30); }
				else if (levelbehavior > 0 && user_monsterlevel > 84) { A_SetTics(31); }
				else if (levelbehavior > 0 && user_monsterlevel > 72) { A_SetTics(32); }
				else if (levelbehavior > 0 && user_monsterlevel > 60) { A_SetTics(33); }
				else if (levelbehavior > 0 && user_monsterlevel > 48) { A_SetTics(34); }
				A_Stop;
			}
			THOR RS 3 
			{
				if (levelbehavior > 0 && user_monsterlevel > 240) { A_SetTics(1); }
				else if (levelbehavior > 0 && user_monsterlevel > 120) { A_SetTics(2); }
				A_FaceTarget;
			}
			THOR T 8 BRIGHT
			{
				if (levelbehavior > 0 && user_monsterlevel > 240) { A_SetTics(4); }
				else if (levelbehavior > 0 && user_monsterlevel > 180) { A_SetTics(5); }
				else if (levelbehavior > 0 && user_monsterlevel > 120) { A_SetTics(6); }
				else if (levelbehavior > 0 && user_monsterlevel > 60) { A_SetTics(7); }

				A_MonsterCrisisCheck;
				A_CrisisSpawnProjectile("LightningRod1", "LightningRod1Crisis", 55, 45, 0, CMF_OFFSETPITCH|CMF_BADPITCH, 0);
				A_CrisisSpawnProjectile("LightningRod1", "LightningRod1Crisis", 55, -45, 0, CMF_OFFSETPITCH|CMF_BADPITCH, 0);
				if (levelbehavior > 0 && user_monsterlevel > 120) 
				{ 
					A_CrisisSpawnProjectile("LightningRod1", "LightningRod1Crisis", 55, 67.5, 0, CMF_OFFSETPITCH|CMF_BADPITCH, 0);
					A_CrisisSpawnProjectile("LightningRod1", "LightningRod1Crisis", 55, -67.5, 0, CMF_OFFSETPITCH|CMF_BADPITCH, 0);
				}
			}
			THOR TT 2 BRIGHT
			{
				A_MonsterCrisisCheck;
				A_CrisisSpawnProjectile("LightningRod1", "LightningRod1Crisis", 55, 45, 0, CMF_OFFSETPITCH|CMF_BADPITCH, 0);
				A_CrisisSpawnProjectile("LightningRod1", "LightningRod1Crisis", 55, -45, 0, CMF_OFFSETPITCH|CMF_BADPITCH, 0);
				if (levelbehavior > 0 && user_monsterlevel > 120) 
				{ 
					A_CrisisSpawnProjectile("LightningRod1", "LightningRod1Crisis", 55, 67.5, 0, CMF_OFFSETPITCH|CMF_BADPITCH, 0);
					A_CrisisSpawnProjectile("LightningRod1", "LightningRod1Crisis", 55, -67.5, 0, CMF_OFFSETPITCH|CMF_BADPITCH, 0);
				}
			}
			TNT1 A 0 A_GiveInventory("ThorCounter", 1)
			TNT1 A 0 A_JumpIfInventory("ThorCounter", 3, "FilibusterEnd")
			THOR SR 3
			{
				if (levelbehavior > 0 && user_monsterlevel > 240) { A_SetTics(1); }
				else if (levelbehavior > 0 && user_monsterlevel > 120) { A_SetTics(2); }
			}
			Goto FilibusterLoop
		FilibusterEnd:
			TNT1 A 0 A_UnSetReflectiveInvulnerable
			TNT1 A 0 A_SetTranslucent(1.0, 0)
			TNT1 A 0 A_ChangeFlag("Visibilitypulse", 0)
			TNT1 A 0 A_TakeInventory("ThorCounter", 9)
			THOR SR 3
			{
				if (levelbehavior > 0 && user_monsterlevel > 240) { A_SetTics(1); }
				else if (levelbehavior > 0 && user_monsterlevel > 120) { A_SetTics(2); }
				A_FaceTarget;
			}
			THOR YZM 10
			{
				if (levelbehavior > 0 && user_monsterlevel > 150) { A_SetTics(5); }
				else if (levelbehavior > 0 && user_monsterlevel > 120) { A_SetTics(6); }
				else if (levelbehavior > 0 && user_monsterlevel > 90) { A_SetTics(7); }
				else if (levelbehavior > 0 && user_monsterlevel > 60) { A_SetTics(8); }
				else if (levelbehavior > 0 && user_monsterlevel > 30) { A_SetTics(9); }
			}
			Goto See
		//attack 4, floor-hugging seekers
		GroundSeekers:
			THOR ZY 5 
			{
				if (levelbehavior > 0 && user_monsterlevel > 180) { A_SetTics(3); }
				else if (levelbehavior > 0 && user_monsterlevel > 90) { A_SetTics(4); }
				A_FaceTarget;
			}
			THOR X 8 BRIGHT
			{
				if (levelbehavior > 0 && user_monsterlevel > 240) { A_SetTics(4); }
				else if (levelbehavior > 0 && user_monsterlevel > 180) { A_SetTics(5); }
				else if (levelbehavior > 0 && user_monsterlevel > 120) { A_SetTics(6); }
				else if (levelbehavior > 0 && user_monsterlevel > 60) { A_SetTics(7); }

				A_CustomMissile("GroundLightningTracer",0,random(-20,20),random(-45,45),0,0);
				A_CustomMissile("GroundLightningTracer",0,random(-20,20),random(-45,45),0,0);
				A_CustomMissile("GroundLightningTracer",0,random(-20,20),random(-45,45),0,0);
				if (levelbehavior > 0 && user_monsterlevel > 120) 
				{
					A_CustomMissile("GroundLightningTracer",0,random(-20,20),random(-45,45),0,0);
					A_CustomMissile("GroundLightningTracer",0,random(-20,20),random(-45,45),0,0);
					A_CustomMissile("GroundLightningTracer",0,random(-20,20),random(-45,45),0,0);
				}
				A_FaceTarget;
			}
			THOR X 8 Bright
			{
				if (levelbehavior > 0 && user_monsterlevel > 240) { A_SetTics(4); }
				else if (levelbehavior > 0 && user_monsterlevel > 180) { A_SetTics(5); }
				else if (levelbehavior > 0 && user_monsterlevel > 120) { A_SetTics(6); }
				else if (levelbehavior > 0 && user_monsterlevel > 60) { A_SetTics(7); }
				A_FaceTarget;
			}
			Goto See
		//attack 5, lightning columns
		LightningColumns:
			THOR RS 5
			{
				if (levelbehavior > 0 && user_monsterlevel > 180) { A_SetTics(3); }
				else if (levelbehavior > 0 && user_monsterlevel > 90) { A_SetTics(4); }
				A_FaceTarget;

				A_CustomMissile("LightningRod3", 0, 0, 0, CMF_AIMOFFSET);
				A_CustomMissile("LightningRod3", 0, 45, random(-25, -15), CMF_AIMOFFSET);
				A_CustomMissile("LightningRod3", 0, -45, random(15, 25), CMF_AIMOFFSET);
				if (levelbehavior > 0 && user_monsterlevel > 120) 
				{
					A_CustomMissile("LightningRod3", 0, 90, random(-50, -30), CMF_AIMOFFSET);
					A_CustomMissile("LightningRod3", 0, -90, random(30, 50), CMF_AIMOFFSET);
				}
			}
			THOR T 5 Bright
			{
				if (levelbehavior > 0 && user_monsterlevel > 180) { A_SetTics(3); }
				else if (levelbehavior > 0 && user_monsterlevel > 90) { A_SetTics(4); }
				A_FaceTarget;
			}
			THOR T 5
			{
				if (levelbehavior > 0 && user_monsterlevel > 180) { A_SetTics(3); }
				else if (levelbehavior > 0 && user_monsterlevel > 90) { A_SetTics(4); }
			}
			Goto See
		Pain:
			THOR Q 5 
			THOR Q 5 
			{
				A_Pain;
				user_painfastchase = 2 * random(1,8);
			}
			TNT1 A 0 A_TakeInventory("ThorCounter", 9)
			Goto See
		Pain.Fist:
			THOR Q 5 
			THOR Q 5 
			{
				A_Pain;
				user_painfastchase = 3 * random(1,8);
			}
			THOR QQ 5 
			TNT1 A 0 A_TakeInventory("ThorCounter", 9)
			Goto See
		Pain.Kick:
			THOR Q 5 
			THOR Q 5 
			{
				A_Pain;
				user_painfastchase = 3 * random(1,8);
			}
			THOR QQQQ 5 
			TNT1 A 0 A_TakeInventory("ThorCounter", 9)
			Goto See
		Pain.GravityHP:
			TNT1 A 0 ACS_NamedExecuteAlways("Hand-GravityAttack",0,0,0,0)
			Goto See
		Pain.GravityMP:
			TNT1 A 0 ACS_NamedExecuteAlways("Hand-GravityAttack",0,1,0,0)
			Goto See
		Pain.GravityHPMP:
			TNT1 A 0 ACS_NamedExecuteAlways("Hand-GravityAttack",0,2,0,0)
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("AddEXP",0,-2,0,1)
			TNT1 A 0 A_SetTranslucent(1.0, 0)
			TNT1 A 0 A_ChangeFlag("Visibilitypulse", 0)
			THR2 A 8 A_Scream
			THR2 B 8
			THR2 C 8
			THR2 D 6
			THR2 E 6
			THR2 F 6
			THR2 D 6
			THR2 E 6
			THR2 F 6
			THR2 D 6
			THR2 E 6
			THR2 F 6
			THR2 G 4 BRIGHT A_PlaySound("thor/thunder")
			THR2 H 6 A_NoBlocking
			THR2 I 6
			THR2 J 6
			THR2 K 6
			THR2 L 6
			THR2 M 6
			THR2 N 6
			TNT1 A 0 ACS_NamedExecuteAlways("MonsterStatsHP_ATK_DEF_SPD",0,1)
			TNT1 A 0 A_SpawnItemEx("ThorCorpse", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION)
			//note that he needs to Stop and "disappear" to shut off his orbiters; a dummy corpse is left instead (see below)
			Stop
	}
}

//used to manage Thor's "looping" attacks
Actor ThorCounter : Inventory
{
	Inventory.MaxAmount 9
}

//dummy corpse
Actor ThorCorpse
{
	Radius 25
	Height 12
	States
	{
		Spawn:
			THR2 O -1
			Loop
	}
}

//orbiters
ACTOR LightningBallC1 : AetheriusProjectileZSC
{
	+Noclip
	Scale .35
	Speed 5
	RENDERSTYLE Add
	Alpha 0.50     
	PROJECTILE
	states
	{
		Spawn:
			ZOT3 AABBCC 1 BRIGHT A_SpawnItemEx("LightningBallC1T", 0, 0, 0, 0, 0, 0, 0, 128)
			ZOT3 A 0 A_CustomMissile("LightningBallC1", 0, 0, 75)
			ZOT3 A 0 A_MissileAttack
			Stop
		Death:
			TNT1 A 0
			Stop
	}
}

ACTOR LightningBallC2 : AetheriusProjectileZSC
{
	+Noclip
	Scale .28
	Speed 5
	RENDERSTYLE Add
	Alpha 0.50     
	PROJECTILE
	states
	{
		Spawn:
			ZOT3 AABBCC 1 BRIGHT A_SpawnItemEx("LightningBallC1T", 0, 0, 0, 0, 0, 0, 0, 128)
			ZOT3 A 0 A_CustomMissile("LightningBallC2", 0, 0, -75)
			ZOT3 A 0 A_MissileAttack
			Stop
		Death:
			TNT1 A 0
			Stop
	}
}

//decorative particle shared by a few actors
ACTOR LightningBallC1T : AetheriusProjectileZSC
{
	RENDERSTYLE ADD
	Alpha 0.4
	PROJECTILE
	+Noclip
	states
	{
		Spawn:
			DFFP GHIJKLMNOPQR 1 BRIGHT
			Stop
	}
}

//limited-range projectile spawned by his "filibuster" attack mode
//has no collision; primary purpose is to continuously spawn damaging particles
ACTOR GroundLightning : AetheriusProjectileZSC
{
	Radius 4
	Height 6
	Speed 20
	//Damage 6
	RENDERSTYLE Add
	Alpha 0.40
	ReactionTime 20//53
	SEESOUND "thor/attackr1"
	PROJECTILE
	+FloorHugger
	+Noclip
	//+Ripper
	states
	{
		Spawn:
			ICWS ABCDEFGHI 1 BRIGHT A_SpawnItemEx("GroundTrail",0,0,0,0,0,0,0,128)
			ICWS A 0 BRIGHT
			ICWS A 0 BRIGHT A_Stop
			Goto ProjectileLoop
		ProjectileLoop:
			ICWS ABCDEFGHI 1 BRIGHT A_CustomMissile("GroundSmoke",0,10,random(0,360),2,random(80,100))
			ICWS A 0 BRIGHT A_CustomMissile("GroundSmoke",0,0,0,2,360)
			TNT1 A 0 A_CountDown
			Loop
		Death:
			ICWS ABCDEFGHI 1 BRIGHT A_CustomMissile("GroundSmoke",0,10,random(0,360),2,random(80,100))
			ICWS A 0 BRIGHT
			Stop
	}
}

//trail spawned by the GroundLightning actor; harmless
ACTOR GroundTrail : AetheriusProjectileZSC
{
	Radius 2
	Height 3
	RENDERSTYLE Add
	Alpha 0.20
	ReactionTime 20//49
	PROJECTILE
	+FloorHugger
	states
	{
		Spawn:
			ICWS JKLMNOPQR 1 BRIGHT
			ICWS A 0 BRIGHT
			TNT1 A 0 A_CountDown
			Loop
		Death:
			ICWS JKLMNOPQR 1 BRIGHT
			Stop
	}
}

//actor spawned by the GroundLightning projectile; deals damage, and occasionally (25%) spawns additional particles
ACTOR GroundSmoke : AetheriusProjectileZSC
{
	Radius 12
	Height 12
	Speed 15
	Damage 1 // 5
	RENDERSTYLE Add
	Alpha 0.30
	SEESOUND "thor/smoke"
	PROJECTILE
	+Ripper
	//+Noclip
	states
	{
		Spawn:
			DFFP ABCDEFGHIJKLMNOPQR 1 BRIGHT
			DFFP A 0 BRIGHT A_Jump(64,1)
			Stop
			DFFP A 1 BRIGHT A_CustomMissile("SmokeLightning",0,0,0,2,-250)
			DFFP A 1 BRIGHT A_CustomMissile("SmokeLightning",0,0,45,2,-250)
			DFFP A 1 BRIGHT A_CustomMissile("SmokeLightning",0,0,90,2,-250)
			DFFP A 1 BRIGHT A_CustomMissile("SmokeLightning",0,0,135,2,-250)
			DFFP A 1 BRIGHT A_CustomMissile("SmokeLightning",0,0,180,2,-250)
			DFFP A 1 BRIGHT A_CustomMissile("SmokeLightning",0,0,225,2,-250)
			DFFP A 1 BRIGHT A_CustomMissile("SmokeLightning",0,0,270,2,-250)
			DFFP A 1 BRIGHT A_CustomMissile("SmokeLightning",0,0,315,2,-250)
			Stop
	}
}

//particles periodically spawned by the GroundSmoke projectile generated by the GroundLightning projectile
ACTOR SmokeLightning : AetheriusProjectileZSC
{
	Radius 16
	Height 16
	Speed 20
	Damage 2 //5
	RENDERSTYLE Add
	Alpha 0.40
	Scale 0.75
	SEESOUND "thor/spawnl"
	DEATHSOUND "thor/hitl"
	ACTIVESOUND "thor/activel"
	PROJECTILE
	states
	{
		Spawn:
			ZOT3 ABC 2 BRIGHT
			Loop
		Death:
			ZOT3 A 0 BRIGHT //A_Explode(50,150)
			Stop
	}
}

//small projectile
ACTOR LightningBallS : AetheriusProjectileZSC
{
	Radius 16
	Height 16
	Speed 20
	Damage 5
	RENDERSTYLE Add
	Alpha 0.40
	Scale 0.75
	SEESOUND "thor/spawnl"
	DEATHSOUND "thor/hitl"
	ACTIVESOUND "thor/activel"
	PROJECTILE
	Decal RevenantScorch
	states
	{
		Spawn:
			ZOT3 ABCE 2 BRIGHT A_SpawnItemEx("LightningBallC1T", 0, 0, 0, 0, 0, 0, 0, 128)
			Loop
		Death:
			ZAPS GHIJKL 2 BRIGHT
			Stop
	}
}

//faster small projectile
ACTOR LightningRod1 : AetheriusProjectileZSC
{
	Radius 24
	Height 24
	Speed 30
	Damage (3*random(1,8))
	RENDERSTYLE Add
	Alpha 0.50
	Scale 0.75
	SEESOUND "thor/spawnl"
	DEATHSOUND "thor/hitl"
	ACTIVESOUND "thor/activel"
	MissileType LightningBallC1T
	MissileHeight 0
	PROJECTILE
	Decal RevenantScorch
	states
	{
		Spawn:
			ZAP6 ABC 2 BRIGHT
			Loop
		Death:
			ZOTS ABCDEF 2 BRIGHT
			Stop
	}
}
Actor LightningRod1CrisisParticle : BaseCrisisParticle
{ 
	States
	{
		Spawn:
			ZAP6 A 0 NoDelay A_CrisisParticleInit
			goto Idle
		Idle:
			"####" ABC 2 Bright A_CrisisParticleDo
			loop
	}
}
ACTOR LightningRod1Crisis : LightningRod1
{
	Damage ((3 * CallACS("CrisisMultiplier")) * random(1,8))
	states
	{
		Spawn:
			ZAP6 ABC 2 BRIGHT A_SpawnItemEx("LightningRod1CrisisParticle",0,0,0,0,0,1.28)
			Loop
		Death:
			ZOTS ABCDEF 2 BRIGHT
			Stop
	}
}

//large projectile; spams smaller projectiles
ACTOR LightningBallL : AetheriusProjectileZSC
{
	Radius 30
	Height 30
	Speed 12
	Damage 20
	RENDERSTYLE Add
	Alpha 0.75
	ReactionTime 6
	SEESOUND "thor/attackr3"
	DEATHSOUND "thor/hitl"
	ACTIVESOUND "thor/activel"
	Decal "Scorch"
	+SeekerMissile
	+DoomBounce
	PROJECTILE
	states
	{
		Spawn:
			ZAP7 ABCDE 1 BRIGHT //A_SeekerMissile(20,20)
			ZAP7 ABCDE 1 BRIGHT //A_SeekerMissile(20,20)
			ZAP7 ABCDE 1 BRIGHT //A_SeekerMissile(20,20)
			ZAP7 ABCDE 1 BRIGHT //A_SeekerMissile(20,20)
			ZAP7 ABCDE 1 BRIGHT //A_SeekerMissile(20,20)
			ZAP7 ABCDE 1 BRIGHT //A_SeekerMissile(20,20)
			ZAP7 ABCDE 1 BRIGHT //A_SeekerMissile(20,20)
			ZAP7 ABCDE 1 BRIGHT //A_SeekerMissile(20,20)
			ZAP7 ABCDE 1 BRIGHT //A_SeekerMissile(20,20)
			ZAP7 ABCDE 1 BRIGHT //A_SeekerMissile(20,20)
		Goto ProjectileLoop
		ProjectileLoop:
			ZAP7 A 1 BRIGHT A_CustomMissile("LightningRod1",10,0,0,0,0)
			ZAP7 B 1 BRIGHT A_CustomMissile("LightningRod1",10,0,20,0,0)
			ZAP7 C 1 BRIGHT A_CustomMissile("LightningRod1",10,0,40,0,0)
			ZAP7 D 1 BRIGHT A_CustomMissile("LightningRod1",10,0,60,0,0)
			ZAP7 E 1 BRIGHT A_CustomMissile("LightningRod1",10,0,80,0,0)
			ZAP7 A 1 BRIGHT A_CustomMissile("LightningRod1",10,0,100,0,0)
			ZAP7 C 1 BRIGHT A_CustomMissile("LightningRod1",10,0,120,0,0)
			ZAP7 D 1 BRIGHT A_CustomMissile("LightningRod1",10,0,140,0,0)
			ZAP7 E 1 BRIGHT A_CustomMissile("LightningRod1",10,0,160,0,0)
			ZAP7 A 1 BRIGHT A_CustomMissile("LightningRod1",10,0,180,0,0)
			ZAP7 B 1 BRIGHT A_CustomMissile("LightningRod1",10,0,200,0,0)
			ZAP7 C 1 BRIGHT A_CustomMissile("LightningRod1",10,0,220,0,0)
			ZAP7 D 1 BRIGHT A_CustomMissile("LightningRod1",10,0,240,0,0)
			ZAP7 E 1 BRIGHT A_CustomMissile("LightningRod1",10,0,260,0,0)
			ZAP7 A 1 BRIGHT A_CustomMissile("LightningRod1",10,0,280,0,0)
			ZAP7 B 1 BRIGHT A_CustomMissile("LightningRod1",10,0,300,0,0)
			ZAP7 C 1 BRIGHT A_CustomMissile("LightningRod1",10,0,320,0,0)
			ZAP7 D 1 BRIGHT A_CustomMissile("LightningRod1",10,0,340,0,0)
			ZAP7 E 1 BRIGHT A_CustomMissile("LightningRod1",10,0,360,0,0)
			ZAP7 ABCDEABCDEABCDEABCDEABCDE 1 BRIGHT//reduces spamminess
			TNT1 A 0 A_CountDown
			Loop
		Death:
			ZAPL ABCDEFG 2 BRIGHT
			Stop
	}
}
//small projectile produced by his "stream" attacks
ACTOR MiniLightningPuff : AetheriusProjectileZSC
{
	Radius 6
	Height 10
	Speed 14
	Damage 1
	PROJECTILE
	RENDERSTYLE ADD
	Alpha 0.67
	SEESOUND "thor/smoke"
	Decal PlasmaScorchLower
	states
	{
		Spawn:
			DFFP ABCDEFGH 4 BRIGHT
		Death:
			DFFP IJKLMNOPQR 4 BRIGHT
			Stop
	}
}

//floor-hugging tracer projectile
ACTOR GroundLightningTracer : AetheriusProjectileZSC
{
	Radius 4
	Height 6
	Speed 10
	Damage 6
	RENDERSTYLE Add
	Alpha 0.40
	ReactionTime 15//34
	SEESOUND "thor/attackr2"
	PROJECTILE
	+FloorHugger
	//+Noclip
	//+Ripper
	+SeekerMissile
	states
	{
		Spawn:
			ICWS ABCDEFGHI 1 BRIGHT A_SpawnItemEx("TracerTrail",0,0,0,0,0,0,0,128)
			TNT1 A 0 A_Jump(128, 2)
			ICWS A 0 BRIGHT A_SeekerMissile(90,90)
			TNT1 A 0 A_CountDown
			Loop
		Death:
			ICWS ABCDEFGHI 5 BRIGHT A_Stop
			ICWS ABCDEFGHI 1 BRIGHT A_CustomMissile("GroundSmoke",0,10,random(0,360),2,random(80,100))
			ICWS ABCDEFGHI 1 BRIGHT A_CustomMissile("GroundSmoke",0,10,random(0,360),2,random(80,100))
			ICWS ABCDEFGHI 1 BRIGHT A_CustomMissile("GroundSmoke",0,10,random(0,360),2,random(80,100))
			ICWS ABCDEFGHI 1 BRIGHT A_CustomMissile("GroundSmoke",0,10,random(0,360),2,random(80,100))
			ICWS ABCDEFGHI 1 BRIGHT A_CustomMissile("GroundSmoke",0,10,random(0,360),2,random(80,100))
			ICWS ABCDEFGHI 1 BRIGHT A_CustomMissile("GroundSmoke",0,10,random(0,360),2,random(80,100))
			ICWS ABCDEFGHI 1 BRIGHT A_CustomMissile("GroundSmoke",0,10,random(0,360),2,random(80,100))
			ICWS ABCDEFGHI 1 BRIGHT A_CustomMissile("GroundSmoke",0,10,random(0,360),2,random(80,100))
			ICWS ABCDEFGHI 1 BRIGHT A_CustomMissile("GroundSmoke",0,10,random(0,360),2,random(80,100))
			ICWS ABCDEFGHI 1 BRIGHT A_CustomMissile("GroundSmoke",0,10,random(0,360),2,random(80,100))
			Stop
	}
}

//modified trail with shorter duration
ACTOR TracerTrail : GroundTrail
{
	ReactionTime 10
}

//this was originally unused; I used it in "LightningColumns"
ACTOR LightningRod3 : AetheriusProjectileZSC
{
	Radius 24
	Height 8
	Damage 5
	Speed 20
	RENDERSTYLE Add
	Alpha 0.40
	SEESOUND "thor/spawnl"
	DEATHSOUND "thor/hitl"
	ACTIVESOUND "thor/activel"
	PROJECTILE
	Decal Scorch
	//+SpawnCeiling
	+FLOORHUGGER
	states
	{
		Spawn:
			ZOT1 ABCDE 2 BRIGHT
			ZOT1 A 0 BRIGHT A_SpawnItemEx("LightningBallC1T", 0, 0, 0, 0, 0, 0, 0, 128)
			Loop
		Death:
			ZOTS ABCDEF 2 BRIGHT A_Explode(60,50,0)
			Stop
	}
}

/////End Thor Decorate\\\\\