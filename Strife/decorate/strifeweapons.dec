ACTOR PoisonBoltNew : PoisonBolt replaces PoisonBolt
{
  Speed 30
  Radius 10
  Height 10
  Damage 50
  Projectile
  +STRIFEDAMAGE
  DamageType "PoisonBolt"
  MaxStepHeight 4
  SeeSound "misc/swish"
  ActiveSound "misc/swish"
  Obituary "$OB_MPPOISONBOLT" // "%o recieved a lethal dose of %k's wrath."
  States
  {
  Spawn:
    ARWP A 10 A_LoopActiveSound
    Loop
  Death:
    AROW A 1
    Stop
  }
}

ACTOR StrifeCrossbowNew : StrifeCrossbow replaces StrifeCrossbow
{
  ConversationID 194
  Weapon.SisterWeapon "StrifeCrossbow2New"
  States
  {
  Spawn:
    CBOW A -1
    Stop
  Ready:
    XBOW A 0 A_ShowElectricFlash
    XBOW A 1 A_WeaponReady
    Wait
  Deselect:
    XBOW A 1 A_Lower
    Loop
  Select:
    XBOW A 1 A_Raise
    Loop
  Fire:
    XBOW A 3 A_ClearFlash
    XBOW B 6 A_FireArrow("ElectricBolt")
    XBOW C 4
    XBOW D 6
    XBOW E 3
    XBOW F 5
    XBOW G 0 A_ShowElectricFlash
    XBOW G 5 A_CheckReload
    Goto Ready+1
  Flash:
    XBOW KLM 5
    Loop
  }
}

ACTOR StrifeCrossbow2New : StrifeCrossbow2 replaces StrifeCrossbow2
{
  States
  {
  Ready:
    XBOW H 1 A_WeaponReady
    Loop
  Deselect:
    XBOW H 1 A_Lower
    Loop
  Select:
    XBOW H 1 A_Raise
    Loop
  Fire:
    XBOW H 3
    XBOW B 6 A_FireArrow("PoisonBoltNew")
    XBOW C 4
    XBOW D 6
    XBOW E 3
    XBOW I 5
    XBOW J 5 A_CheckReload
    Goto Ready
  Flash:
    Stop
  }
}

//**********
// SIGIL
//**********
// NOTE: NN stands for Non-Native because we aren't inheriting from the "Sigil" actor.

Actor NNSigil : Weapon
{
   Weapon.Kickback 100
   Weapon.SelectionOrder 4000
   +WEAPON.CHEATNOTWEAPON
   Tag "$TAG_SIGIL"
   States
   {
   Deselect:
      "----" A 1 Bright A_Lower
      Loop
   //
   Select:
      TNT1 A 1 A_JumpIf(CallACS("NNSigilPieceCount") == 5, "Select5")
      TNT1 A 0 A_JumpIf(CallACS("NNSigilPieceCount") == 5, "Select5")
      TNT1 A 0 A_JumpIf(CallACS("NNSigilPieceCount") == 4, "Select4")
      TNT1 A 0 A_JumpIf(CallACS("NNSigilPieceCount") == 3, "Select3")
      TNT1 A 0 A_JumpIf(CallACS("NNSigilPieceCount") == 2, "Select2")
      TNT1 A 0 A_JumpIf(CallACS("NNSigilPieceCount") == 1, "Select1")
   Select1:
      SIGH A 1 Bright A_Raise
      Loop
   Select2:
      SIGH B 1 Bright A_Raise
      Loop
   Select3:
      SIGH C 1 Bright A_Raise
      Loop
   Select4:
      SIGH D 1 Bright A_Raise
      Loop
   Select5:
      SIGH E 1 Bright A_Raise
      Loop
   //
      //Well, it seems using "----" A in 'Ready' makes ZDoom refuse
      //to give you the item. Interesting.
   Ready:
      SIGH "#" 1 Bright A_WeaponReady
      Loop
   //
   Fire:
      "----" A 0 A_JumpIf(CallACS("NNSigilPieceCount") == 5, "Fire5")
      "----" A 0 A_JumpIf(CallACS("NNSigilPieceCount") == 4, "Fire4")
      "----" A 0 A_JumpIf(CallACS("NNSigilPieceCount") == 3, "Fire3")
      "----" A 0 A_JumpIf(CallACS("NNSigilPieceCount") == 2, "Fire2")
      "----" A 0 A_JumpIf(CallACS("NNSigilPieceCount") == 1, "Fire1")
   Fire1:
      "----" A 0 A_PlaySound("weapons/sigilcharge", CHAN_WEAPON)
      "----" A 18 Bright A_Light2
      "----" A 3 Bright A_GunFlash
      TNT1 A 0 A_PlaySound("weapons/sigilcharge", CHAN_WEAPON)
      TNT1 A 0 ACS_NamedExecuteAlways("NNSigilDamageUser", 0, 4)
      SIGH A 15 A_FireCustomMissile("NNSigilLightningSpot", 0, 0)
      Goto Ready
   Fire2:
      "----" A 0 A_PlaySound("weapons/sigilcharge", CHAN_WEAPON)
      "----" A 18 Bright A_Light2
      "----" A 3 Bright A_GunFlash
      TNT1 A 0 A_PlaySound("weapons/sigilcharge", CHAN_WEAPON)
      TNT1 A 0 ACS_NamedExecuteAlways("NNSigilDamageUser", 0, 8)
      SIGH B 15 A_FireCustomMissile("SpectralLightningH1", 0, 0)
      Goto Ready
   Fire3:
      "----" A 0 A_PlaySound("weapons/sigilcharge", CHAN_WEAPON)
      "----" A 18 Bright A_Light2
      "----" A 3 Bright A_GunFlash
      TNT1 A 0 A_PlaySound("weapons/sigilcharge", CHAN_WEAPON)
      TNT1 A 0 ACS_NamedExecuteAlways("NNSigilDamageUser", 0, 12)
      TNT1 A 0 A_GiveInventory("NNSigil3AttackCounter", 20)
      TNT1 A 0 A_SetAngle(angle-90)
   Fire3Loop:
      TNT1 A 0 A_SetAngle(angle+9)
      TNT1 A 0 A_FireCustomMissile("SpectralLightningBall1", 0, 0)
      TNT1 A 0 A_TakeInventory("NNSigil3AttackCounter", 1)
      TNT1 A 0 A_JumpIfInventory("NNSigil3AttackCounter", 1, "Fire3Loop")
      SIGH C 15 A_SetAngle(angle-90)
      Goto Ready
   Fire4:
      "----" A 0 A_PlaySound("weapons/sigilcharge", CHAN_WEAPON)
      "----" A 18 Bright A_Light2
      "----" A 3 Bright A_GunFlash
      TNT1 A 0 A_PlaySound("weapons/sigilcharge", CHAN_WEAPON)
      TNT1 A 0 ACS_NamedExecuteAlways("NNSigilDamageUser", 0, 16)
      SIGH D 15 A_FireCustomMissile("NNSigilLightningBigV", 0, 0)
      Goto Ready
   Fire5:
      "----" A 0 A_PlaySound("weapons/sigilcharge", CHAN_WEAPON)
      "----" A 18 Bright A_Light2
      "----" A 3 Bright A_GunFlash
      TNT1 A 0 A_PlaySound("weapons/sigilcharge", CHAN_WEAPON)
      TNT1 A 0 ACS_NamedExecuteAlways("NNSigilDamageUser", 0, 20)
      SIGH E 15 A_FireCustomMissile("SpectralLightningBigBall1", 0, 0)
      Goto Ready
   //
   Flash:
      SIGF A 4 Bright A_Light2
      SIGF B 6 Bright A_LightInverse
      SIGF C 4 Bright A_Light1
      TNT1 A 0 A_Light0
      Stop
   }
}

ACTOR SpectralLightningBaseNew : SpectralLightningBase replaces SpectralLightningBase { +SPECTRAL DamageType "Sigil" }
ACTOR SpectralLightningBall1New : SpectralLightningBall1 replaces SpectralLightningBall1 { +SPECTRAL DamageType "Sigil" }
ACTOR SpectralLightningBall2New : SpectralLightningBall2 replaces SpectralLightningBall2 { +SPECTRAL DamageType "Sigil" }
ACTOR SpectralLightningDeath1New : SpectralLightningDeath1 replaces SpectralLightningDeath1 { +SPECTRAL DamageType "Sigil" }
ACTOR SpectralLightningDeath2New : SpectralLightningDeath2 replaces SpectralLightningDeath2 { +SPECTRAL DamageType "Sigil" }
ACTOR SpectralLightningDeathShortNew : SpectralLightningDeathShort replaces SpectralLightningDeathShort { +SPECTRAL DamageType "Sigil" }
ACTOR SpectralLightningBigBall1New : SpectralLightningBigBall1 replaces SpectralLightningBigBall1 { +SPECTRAL DamageType "Sigil" }
ACTOR SpectralLightningBigBall2New : SpectralLightningBigBall2 replaces SpectralLightningBigBall2 { +SPECTRAL DamageType "Sigil" }
ACTOR SpectralLightningV1New : SpectralLightningV1 replaces SpectralLightningV1 { +SPECTRAL DamageType "Sigil" DamageType "SpectralLow" }
ACTOR SpectralLightningV2New : SpectralLightningV2 replaces SpectralLightningV2 { +SPECTRAL DamageType "Sigil" DamageType "SpectralLow" }
ACTOR SpectralLightningH1New : SpectralLightningH1 replaces SpectralLightningH1 { +SPECTRAL DamageType "Sigil" }
ACTOR SpectralLightningH2New : SpectralLightningH2 replaces SpectralLightningH2 { +SPECTRAL DamageType "Sigil" }
ACTOR SpectralLightningH3New : SpectralLightningH3 replaces SpectralLightningH3 { +SPECTRAL DamageType "Sigil" }
ACTOR SpectralLightningSpotNew : SpectralLightningSpot replaces SpectralLightningSpot { +SPECTRAL DamageType "Sigil" }

Actor NNSigil3AttackCounter : Inventory
{
   Inventory.MaxAmount 20
}

DamageType NNSigilUse
{
   NoArmor
}

Actor NNSigilLightningSpot : SpectralLightningSpot
{
   +SPECTRAL //This flag is needed, otherwise the SpectralLightningV1/V2
   DamageType "Sigil"
   States    //actors won't damage the spectres. Wierd.
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_TransferPointer(AAPTR_TARGET, AAPTR_DEFAULT, AAPTR_PLAYER_GETTARGET, AAPTR_TRACER)
      TNT1 A 0 A_CheckFlag("SHOOTABLE", 3, AAPTR_TRACER)
      TNT1 A 0 A_Warp(AAPTR_TARGET) //Try to stick to the player's feet.
      ZAP5 A 4 Bright A_ChangeVelocity(28,0,0, CVF_RELATIVE|CVF_REPLACE)
      Goto Super::Spawn+1
      ZAP5 A 4 Bright A_Warp(AAPTR_TRACER, 0,0,0, 0, WARPF_NOCHECKPOSITION|WARPF_STOP|WARPF_TOFLOOR)
      Goto Super::Spawn+1
   }
}

Actor NNSigilLightningBigV : SpectralLightningBigV1
{
   Decal "BaronScorch"
   DamageType "Sigil"
   States
   {
   Spawn:
      TNT1 A 0
      TNT1 A 0 A_TransferPointer(AAPTR_TARGET, AAPTR_DEFAULT, AAPTR_PLAYER_GETTARGET, AAPTR_TRACER)
      TNT1 A 0 A_CheckFlag("SHOOTABLE", 2, AAPTR_TRACER)
      ZOT2 A 4 Bright A_ChangeVelocity(28,0,0, CVF_RELATIVE)
      Goto Super::Spawn+1
      ZOT2 A 4 Bright
      Goto Super::Spawn+1
   }
}