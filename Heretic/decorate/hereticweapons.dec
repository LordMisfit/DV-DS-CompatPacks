ACTOR GoldWandNew : GoldWand replaces GoldWand
{
  Weapon.AmmoType1 "GoldWandAmmo"
  Weapon.AmmoType2 "GoldWandAmmo"
  Weapon.AmmoUse1 1  
  Weapon.AmmoUse2 1  
  States
  {
  Spawn:
    GWAN A -1
    Stop
  Ready:
    GWND A 1 A_WeaponReady
    Loop
  Deselect:
    GWND A 1 A_Lower
    Loop
  Select:
    GWND A 1 A_Raise
    Loop
  Fire:
    GWND B 3 
    GWND C 5 // A_FireGoldWandPL1
    {
	 A_PlaySound("weapons/wandhit",1);
     A_FireBullets(5.625,0,1,(6+random(1,8)),"GoldWandPuff1",1);
	}
    GWND D 3
    GWND D 0 A_ReFire
    Goto Ready
  AltFire:
    GWND B 3 
    GWND C 4 // A_FireGoldWandPL2
    {
	 A_PlaySound("weapons/wandhit",1);
     A_SetAngle(angle-5.625);
	 A_FireCustomMissile("GoldWandFX2",0,0);
	 A_FireBullets(0,0,1,random(1,8),"GoldWandPuff2",0);
     A_SetAngle(angle+2.8125);
	 A_FireBullets(0,0,1,random(1,8),"GoldWandPuff2",0);
     A_SetAngle(angle+2.8125);
	 A_FireBullets(0,0,1,random(1,8),"GoldWandPuff2",0);
     A_SetAngle(angle+2.8125);
	 A_FireBullets(0,0,1,random(1,8),"GoldWandPuff2",0);
     A_SetAngle(angle+2.8125);
	 A_FireBullets(0,0,1,random(1,8),"GoldWandPuff2",0);
	 A_FireCustomMissile("GoldWandFX2",0,1);
     A_SetAngle(angle-5.625);
	}
    GWND D 3
    GWND D 0 A_ReFire
    Goto Ready
  }
}


ACTOR CrossbowNew : Crossbow replaces Crossbow
{
  Weapon.AmmoUse1 1
  Weapon.AmmoUse2 1
  Weapon.AmmoType1 "CrossbowAmmo"
  Weapon.AmmoType2 "CrossbowAmmo"
  States
  {
  Spawn:
    WBOW A -1
    Stop
  Ready:
    CRBW AAAAAABBBBBBCCCCCC 1 A_WeaponReady
    Loop
  Deselect:
    CRBW A 1 A_Lower
    Loop
  Select:
    CRBW A 1 A_Raise
    Loop
  Fire:
    CRBW D 6 // A_FireCrossbowPL1
	{
     A_FireCustomMissile ("CrossbowFX1",    0, 1,  0, 3);
     A_FireCustomMissile ("CrossbowFX3", -4.5, 0, -2, 3);
     A_FireCustomMissile ("CrossbowFX3",  4.5, 0,  2, 3);
	}
    CRBW EFGH 3
    CRBW AB 4
    CRBW C 5 A_ReFire
    Goto Ready
  AltFire:
    CRBW D 5 // A_FireCrossbowPL2
	{
     A_FireCustomMissile ("CrossbowFX2",    0, 1,  0, 3);
     A_FireCustomMissile ("CrossbowFX2", -4.5, 0, -2, 3);
     A_FireCustomMissile ("CrossbowFX2",  4.5, 0,  2, 3);
     A_FireCustomMissile ("CrossbowFX3",   -9, 0, -4, 3);
     A_FireCustomMissile ("CrossbowFX3",    9, 0,  4, 3);
	}
    CRBW E 3
    CRBW F 2
    CRBW G 3
    CRBW H 2
    CRBW A 3
    CRBW B 3
    CRBW C 4 A_ReFire
    Goto Ready
  }
}

ACTOR DragonClawNew : Blaster replaces Blaster
{
  Weapon.AmmoUse1 1
  Weapon.AmmoUse2 1
  Weapon.AmmoType1 "BlasterAmmo"
  Weapon.AmmoType2 "BlasterAmmo"
  States
  {
  Spawn:
    WBLS A -1
    Stop
  Ready:
    BLSR A 1 A_WeaponReady
    Loop
  Deselect:
    BLSR A 1 A_Lower
    Loop
  Select:
    BLSR A 1 A_Raise
    Loop
  Fire:
    BLSR BC 3
  Hold:
    BLSR D 2 // A_FireBlasterPL1
    {
	 A_PlaySound("weapons/blastershoot",1);
     A_FireBullets(5.625,0,1,(4*random(1,8)),"BlasterPuff",1);
	}
    BLSR CB 2
    BLSR A 0 A_ReFire
    Goto Ready
  AltFire:
    BLSR BC 0
  AltHold:
    BLSR D 3 A_FireCustomMissile("BlasterFX1")
    BLSR CB 4
    BLSR A 0 A_ReFire
    Goto Ready
  }
}